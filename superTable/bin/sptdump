#!/bin/sh
# La ligne "exec ..." est un commentaire pour tclsh, pas pour /bin/sh
# Normalement, "Bourne shell" trouve "tclsh" si celui-ci est bien installé
# (dans un des répertoires $PATH)
# \
exec tclsh "$0" ${1+"$@"}

# mettre dans ~/.tclshrc la ligne sans "set rien {" ni "}"
set rien {
    set fidev_tclDir /bob/fab/A/fidev/Tcl
    source $fidev_tclDir/pkgIndex.tcl
    package provide fidev 1.1
    set env(FIDEV_EXPERIMENTAL) /bob/fab/C/fidev-unknown-Linux-2.2.12-20-cc-stable/lib
}

package require fidev
package require superTable 1.5 

  # sans argument : revoie la syntaxe
    if {$argc < 1} {
        puts stderr "- Usage : $argv0 \[fichier.spt \[nomDeSupertable \[col1 ... coln\]\]\]"
        puts stderr "- nomDeSupertable peut être approché (globing)"
        puts stderr "- Si le fichier ne contient qu'une superTable,"
        puts stderr "  on peut remplacer nomDeSupertable par \"*\" (écrire les guillemets pour shunter l'interpréteur de commande Unix)"
        exit 1
    }

  # nom du fichier à lire
    set nameOfSptFile [lindex $argv 0]

  # sans autre argument
    if {$argc == 1} {
        set lignes [::superTable::getLines $nameOfSptFile] ;# lignes
        set tlimits [::superTable::marqueTables $lignes]   ;# index des lignes @@
        set noms {}                                    ;# liste des noms
        foreach i $tlimits {
          # on supprime @@ de la ligne et on l'ajoute à la liste noms
            lappend noms [string range [lindex $lignes $i] 2 end]
        }
          # impression des noms de superTables
        puts $noms
        exit 0
    }

    set nameOfTable [lindex $argv 1]
    set cols [lrange $argv 2 end]

  # lecture de la superTable dans le tableau a
  # iii contient une triple liste
    
    set iii [::superTable::fileToTable a $nameOfSptFile nameOfTable {}]

  # sans arguments noms de colonnes
    if {$cols == {}} {
      # impression d'une double liste : colonnes communes et non communes
        puts [lrange $iii 1 2]
        exit 0
    } 

  # avec arguments noms de colonnes : pour chaque index de ligne
    foreach i [lindex $iii 0] {
        set first 1
      # pour chaque colonne
        foreach t $cols {
            if {$first} {
                set first 0
            } else {
                puts -nonewline "\t"
            }
          # impression de la colonne
            puts -nonewline $a([list $i $t])
        }
        puts {}
    }

    exit 0
